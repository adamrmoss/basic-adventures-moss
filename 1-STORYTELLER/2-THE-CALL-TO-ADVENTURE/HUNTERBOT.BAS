100 REM --- HUNTERBOT ---
110 REM -- SETUP --
120 PSGINIT
130 SCREEN 1

140 REM -- INTRO --
150 PRINT "YOU'RE BEING CHASED BY A HUNTERBOT."
160 PSGPLAY 0, "O3 L16 AB L2 > C L4 < BG L2 C"
170 PRINT
180 PRINT "TWO ESCAPE ROUTES ARE AVAILABLE.  CHOOSE WISELY."
190 PRINT "TAKE THE 'ALLEYWAY' OR ENTER THE SHOPPING 'MALL'";
200 INPUT C$
210 IF C$="ALLEYWAY" THEN GOTO 250
220 IF C$="MALL" THEN GOTO 360
230 PRINT "PLEASE CHOOSE EITHER 'ALLEYWAY' OR 'MALL'"
240 GOTO 200

250 REM -- ALLEYWAY --
260 PRINT
270 PRINT "YOU DASH INTO THE DARK ALLEYWAY."
280 GOSUB 1290
290 PRINT "DEAD END AHEAD!"
300 PRINT "WILL YOU 'HIDE' IN A DUMPSTER OR PICK UP A PIPE AND 'FIGHT'";
310 INPUT C$
320 IF C$="HIDE" THEN GOTO 470
330 IF C$="FIGHT" THEN GOTO 520
340 PRINT "PLEASE CHOOSE EITHER 'HIDE' OR 'FIGHT'"
350 GOTO 310

360 REM -- SHOPPING MALL --
370 PRINT
380 PRINT "YOU RUSH INTO THE SHOPPING MALL."
390 GOSUB 1290
400 PRINT "LOUD CROWDS OFFER COVER.  WHERE DO YOU GO?"
410 PRINT "DO YOU HEAD FOR THE 'ARCADE' OR RUN UP THE 'ESCALATOR'";
420 INPUT C$
430 IF C$="ARCADE" THEN GOTO 630
440 IF C$="ESCALATOR" THEN GOTO 690
450 PRINT "PLEASE CHOOSE EITHER 'ARCADE' OR 'ESCALATOR'"
460 GOTO 420

470 REM -- HIDE --
480 PRINT
490 PRINT "YOU HIDE IN THE DUMPSTER.  THE HUNTERBOT PASSES BY."
500 PRINT "YOU'RE SAFE FOR NOW.  GOOD JOB!"
510 GOTO 1210

520 REM -- FIGHT --
530 PRINT
540 PRINT "YOU GRAB THE PIPE AND STAND YOUR GROUND."
550 GOSUB 1290
560 PRINT "THE HUNTERBOT ATTACKS.  CAN YOU OVERCOME IT?"
570 PRINT "WILL YOU STRIKE AT ITS 'HEAD' OR TRY TO 'DISARM' IT";
580 INPUT C$
590 IF C$="HEAD" THEN GOTO 800
600 IF C$="DISARM" THEN GOTO 850
610 PRINT "PLEASE CHOOSE EITHER 'HEAD' OR 'DISARM'"
620 GOTO 580

630 REM -- ARCADE --
640 PRINT
650 PRINT "YOU ENTER THE ARCADE AND START PLAYING AFTERBURNER."
660 PRINT "THE HUNTERBOT LOSES YOU IN THE SEA OF PEOPLE."
670 PRINT "YOU HAVE ESCAPED THE HUNTERBOT \XC3 FOR NOW!"
680 GOTO 1210

690 REM -- ESCALATOR --
700 PRINT
710 PRINT "YOU RUN UP THE ESCALATOR, HOPING TO LOSE THE HUNTERBOT."
720 GOSUB 1290
730 PRINT "AT THE TOP, YOU FIND A ROOFTOP EXIT.  DARE YOU RISK IT?"
740 PRINT "DO YOU 'JUMP' TO THE ADJACENT ROOF OR GO 'BACK' INTO THE MALL";
750 INPUT C$
760 IF C$="JUMP" THEN GOTO 910
770 IF C$="BACK" THEN GOTO 1020
780 PRINT "PLEASE CHOOSE EITHER 'JUMP' OR 'BACK'"
790 GOTO 750

800 REM -- HEAD --
810 PRINT
820 PRINT "YOU STRIKE THE HUNTERBOT'S HEAD AND DISABLE IT!"
830 PRINT "VICTORY!"
840 GOTO 1210

850 REM -- DISARM --
860 PRINT
870 PRINT "YOU TRY TO DISABLE THE HUNTERBOT BUT FAIL."
880 PRINT "IT OVERPOWERS YOU AND PINS YOU DOWN."
890 PRINT "THE HUNTERBOT ELBOWS YOU IN THE FACE TO DEATH."
900 GOTO 1250

910 REM -- JUMP --
920 PRINT
930 PRINT "YOU JUMP TO THE ADJACENT ROOF AND LAND SAFELY."
940 GOSUB 1290
950 PRINT "YOU FIND YOURSELF IN A CONSTRUCTION SITE."
960 PRINT "DO YOU 'HIDE' AMONG THE STRUCTURES OR FIND SOMETHING TO 'THROW'";
970 INPUT C$
980 IF C$="HIDE" THEN GOTO 1080
990 IF C$="THROW" THEN GOTO 1140
1000 PRINT "PLEASE CHOOSE EITHER 'HIDE' OR 'THROW'"
1010 GOTO 970

1020 REM -- BACK --
1030 PRINT
1040 PRINT "YOU GO BACK INTO THE MALL, ";
1050 PRINT "AND THE HUNTERBOT GRABS YOU BY THE THROAT."
1060 PRINT "HE CRUSHES YOUR TRACHEA."
1070 GOTO 1250

1080 REM -- HIDE --
1090 PRINT
1100 PRINT "YOU HIDE AMONG THE STRUCTURES AND STAY QUIET."
1110 PRINT "THE HUNTERBOT PASSES BY, UNAWARE OF YOUR LOCATION."
1120 PRINT "YOU'VE OUTSMARTED IT.  WELL DONE!"
1130 GOTO 1210

1140 REM -- THROW --
1150 PRINT
1160 PRINT "YOU FIND A HEAVY OBJECT AND THROW IT AT THE HUNTERBOT."
1170 PRINT "YOU MANAGE TO TEMPORARILY DISABLE IT."
1180 GOSUB 1290
1190 PRINT "BUT IT'S NOT DOWN FOR LONG.  YOU MUST KEEP RUNNING."
1200 GOTO 170

1210 REM -- SUCCESS --
1220 PSGPLAY 0, "O3 L16 AB L2 > C L4 < BG L4 > E.D."
1230 PRINT "THE END"
1240 END

1250 REM -- FAILURE
1260 PSGPLAY 0, "L4 GF#F L2 E"
1270 PRINT "THE END"
1280 END

1290 REM -- SUB -- NEXT CHOICE --
1300 PSGPLAY 0, "L12 O3 CCC L2 F"
1310 RETURN
