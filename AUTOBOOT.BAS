100 REM --- AUTOBOOT ---
110 REM -- LOAD DATA --
120 LET VN=3
130 LET CN=5
140 DIM VO$(VN)
150 DIM CH$(VN, CN)
160 READ T$
170 FOR I=1 TO VN
180 : READ VO$(I)
190 : FOR J=1 TO CN
200 : : READ CH$(I, J)
210 : NEXT J
220 NEXT I

230 REM -- SWITCH TO VOLUME --
240 GOSUB 430
250 FOR I=1 TO VN
260 : PRINT VO$(I)
270 NEXT I
280 PRINT: PRINT "WHICH VOLUME?"
290 GOSUB 480: LET V = K
300 DOS "CD:"+VO$(V)

310 REM -- SWITCH TO CHAPTER --
320 GOSUB 430
330 FOR J=1 TO CN
340 : PRINT CH$(V, J)
350 NEXT J
360 PRINT: PRINT "WHICH CHAPTER?"
370 GOSUB 480: LET CH = K
380 DOS "CD:"+CH$(V, CH)

390 REM -- SHOW FILES --
400 GOSUB 430
410 DOS "$"
420 NEW

430 REM -- SUB - BANNER AND TITLE --
440 BANNER
450 LOCATE 10, (80-LEN(T$))/2
460 PRINT T$: PRINT
470 RETURN

480 REM -- SUB - GET NUMBER KEY --
490 GET K$
500 IF K$ < "1" OR K$ > "5" THEN GOTO 490
510 LET K = VAL(K$)
520 RETURN
530 REM -- DATA --
540 DATA "-- BASIC ADVENTURES WITH PROFESSOR MOSS V1.1 --"
550 REM - VOLUME 1 -
560 DATA "1-STORYTELLER"
570 DATA "1-GREETINGS-AND-SALUTATIONS", "2-THE-CALL-TO-ADVENTURE"
580 DATA "3-RANDOM-CHAOS", "4-READING-THE-SCROLLS", "5-TELLING-TALES"
590 REM - VOLUME 2 -
600 DATA "2-PROGRAMMER"
610 DATA "1-BITS-AND-BYTES", "2-GETTING-THE-BAND-BACK-TOGETHER"
620 DATA "3-PAINTING-BY-NUMBERS", "4-CYCLES-OF-THE-STARS"
630 DATA "5-CONNECTING-THE-DOTS"
640 REM - VOLUME 3 -
650 DATA "3-ENGINEER"
660 DATA "1-SIMULATION-OF-LIFE", "2-ROBOT-FRIEND", "3-COLONEL-VERA"
670 DATA "4-THE-REAL-WORLD", "5-THE-ANALOG-REBELLION"
