100 REM --- AUTOBOOT ---
110 REM -- LOAD DATA --
120 LET VN=3
130 LET CN=5
140 DIM VO$(VN)
150 DIM CH$(VN, CN)
160 FOR I=1 TO VN
170 : READ VO$(I)
180 : FOR J=1 TO CN
190 : : READ CH$(I, J)
200 : NEXT J
210 NEXT I

220 REM -- SWITCH TO CHAPTER --
230 BANNER
240 FOR I=1 TO VN
250 : PRINT VO$(I)
260 NEXT I
270 PRINT
280 PRINT "WHICH VOLUME?"
290 GOSUB 440: LET V = K
300 DOS "CD:"+VO$(V)

310 REM -- SWITCH TO CHAPTER --
320 BANNER
330 FOR J=1 TO CN
340 : PRINT CH$(V, J)
350 NEXT J
360 PRINT
370 PRINT "WHICH CHAPTER?"
380 GOSUB 440: LET CH = K
390 DOS "CD:"+CH$(V, CH)

400 REM -- SHOW FILES --
410 BANNER
420 DOS "$"
430 NEW
440 REM -- SUB - GET NUMBER KEY --
450 GET K$
460 IF K$ < "1" OR K$ > "5" THEN GOTO 450
470 LET K = VAL(K$)
480 RETURN

490 REM -- DATA --
500 REM - VOLUME 1 -
510 DATA "1-STORYTELLER"
520 DATA "1-GREETINGS-AND-SALUTATIONS", "2-THE-CALL-TO-ADVENTURE"
530 DATA "3-RANDOM-CHAOS", "4-PAINTING-BY-NUMBERS", "5-TELLING-TALES"
540 REM - VOLUME 2 -
550 DATA "2-PROGRAMMER"
560 DATA "1-BITS-AND-BYTES", "2-GETTING-THE-BAND-BACK-TOGETHER"
570 DATA "3-READING-THE-SCROLLS", "4-CYCLES-OF-THE-STARS"
580 DATA "5-CONNECTING-THE-DOTS"
590 REM - VOLUME 3 -
600 DATA "3-ENGINEER"
610 DATA "1-SIMULATION-OF-LIFE", "2-ROBOT-FRIEND", "3-COLONEL-VERA"
620 DATA "4-THE-REAL-WORLD", "5-THE-ANALOG-REBELLION"
