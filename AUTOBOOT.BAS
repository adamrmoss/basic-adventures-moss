100 REM --- AUTOBOOT ---
110 REM -- LOAD DATA --
120 LET VN=3
130 LET CN=5
140 DIM VO$(VN)
150 DIM CH$(VN, CN)
160 READ T$
170 FOR I=1 TO VN
180 : READ VO$(I)
190 : FOR J=1 TO CN
200 : : READ CH$(I, J)
210 : NEXT J
220 NEXT I

230 REM -- SWITCH TO VOLUME --
240 GOSUB 450
250 FOR I=1 TO VN
260 : PRINT VO$(I)
270 NEXT I
280 PRINT
290 PRINT "WHICH VOLUME?"
300 GOSUB 500: LET V = K
310 DOS "CD:"+VO$(V)

320 REM -- SWITCH TO CHAPTER --
330 GOSUB 450
340 FOR J=1 TO CN
350 : PRINT CH$(V, J)
360 NEXT J
370 PRINT
380 PRINT "WHICH CHAPTER?"
390 GOSUB 500: LET CH = K
400 DOS "CD:"+CH$(V, CH)

410 REM -- SHOW FILES --
420 GOSUB 450
430 DOS "$"
440 NEW

450 REM -- SUB - BANNER AND TITLE --
460 BANNER
470 LOCATE 10, (80-LEN(T$))/2
480 PRINT T$: PRINT
490 RETURN

500 REM -- SUB - GET NUMBER KEY --
510 GET K$
520 IF K$ < "1" OR K$ > "5" THEN GOTO 510
530 LET K = VAL(K$)
540 RETURN

550 REM -- DATA --
560 DATA "-- BASIC ADVENTURES WITH PROFESSOR MOSS V1.1 --"
570 REM - VOLUME 1 -
580 DATA "1-STORYTELLER"
590 DATA "1-GREETINGS-AND-SALUTATIONS", "2-THE-CALL-TO-ADVENTURE"
600 DATA "3-RANDOM-CHAOS", "4-PAINTING-BY-NUMBERS", "5-TELLING-TALES"
610 REM - VOLUME 2 -
620 DATA "2-PROGRAMMER"
630 DATA "1-BITS-AND-BYTES", "2-GETTING-THE-BAND-BACK-TOGETHER"
640 DATA "3-READING-THE-SCROLLS", "4-CYCLES-OF-THE-STARS"
650 DATA "5-CONNECTING-THE-DOTS"
660 REM - VOLUME 3 -
670 DATA "3-ENGINEER"
680 DATA "1-SIMULATION-OF-LIFE", "2-ROBOT-FRIEND", "3-COLONEL-VERA"
690 DATA "4-THE-REAL-WORLD", "5-THE-ANALOG-REBELLION"
